plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'jacoco'
}

description = "A Gradle plugin that compiles the Java files generated by the `spring-wiremock-stub-generator` annotation processor and builds a JAR file."

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

gradlePlugin {
    plugins {
        wiremockStubJarPlugin {
            id = 'io.github.lsd-consulting.gradle.wiremock-stub-jar'
            implementationClass = 'io.lsdconsulting.stub.plugin.WiremockStubJarPlugin'
        }
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
    finalizedBy jacocoTestReport
    afterSuite { desc, result ->
        if (!desc.parent) {
            logger.lifecycle("Test results: (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)")
            if (result.testCount == 0) {
                throw new IllegalStateException("No tests were found. Failing the build")
            }
        }
    }
}

jacoco {
    toolVersion = "0.8.7"
}

jacocoTestReport {
    reports {
        xml.enabled(true)
        html.enabled(true)
        html.destination(project.provider(() -> new File("${project.buildDir}/reports/coverage")))
    }
    dependsOn test
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId "${rootProject.name}-${project.name}"
            from components.java
        }
    }
}

java {
    withSourcesJar()
//    withJavadocJar()
}
